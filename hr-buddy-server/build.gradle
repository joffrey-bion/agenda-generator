buildscript {
    ext {
        kotlinVersion = '1.2.70'
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.19.2')
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

dependencies {
    compile project(':agenda-generator')
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlin:kotlin-reflect'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

// packages the frontend app within the jar
bootJar {
    from('../hr-buddy-ui/dist/hr-buddy-ui') {
        into 'static'
    }
}
// make sure we build the frontend before creating the jar
bootJar.dependsOn(':hr-buddy-ui:assemble')

docker {
    name "${rootProject.dockerUser}/${bootJar.baseName}"
    tags 'latest', "${project.version}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}
tasks.docker.dependsOn(tasks.build)
tasks.dockerPush.dependsOn { tasks.dockerTag }
